// cfos_arc_Coloc.ijm
// IJ macro to measure object-based colocalization of nuclei
// Theresa Swayne, Columbia University, 2017
// input: two single-channel single-slice images, and corresponding ROIsets from cfos_analysis macro, in same directory
// output: mask of overlapping cells, and custom table: 
//     # C1 cells, # C2 cells, # C1+C2 cells, % C1 cells with C2 label, % C2 cells with C1 label.
// 
// usage: Run the nuclear analysis first to get the ROI sets.  Have them in the same directory as the channel images. 
//        Then run the macro, and when prompted select the C1 image. They need to have names starting with C1 and C2 
// 		  as generated by the split_channels macro.


// SETUP

// these values can be changed if they are also changed in the channel analysis macros
CELLMIN = 50 // microns^2
CELLMAX = 300 // microns^2

// make sure the correct channel is opened to begin with
channel = "0";
while (channel != "1") 
	{
	open();
	title = getTitle();
	channel = substring(title, 1, 2);
	if (channel != "1")
		{  
		Dialog.create("Input error");
		Dialog.addMessage("Please open a C1 image."); 
		Dialog.show();
		close();
		}
	}

// parsing file names
path = getDirectory("image");
dotIndex = indexOf(title, ".");
basename = substring(title, 3, dotIndex); // omitting the channel name
roiname = "Coloc-"+ basename;


function makeMask(image, channel) 
	{
	// creates and saves a mask from a ROIset and image that are open
	// leaves the original image and mask open
	
	roiManager("deselect"); // select none 
	roiManager("Combine"); // if none are selected, all will be combined
	roiManager("Delete"); // delete individual rois
	roiManager("Add"); // the combined ROI so there is now only one in the list
	roiManager("Select", 0); // the combined roi
	run("Create Mask");
	roiManager("reset");

	maskName = "C"+channel+"-Mask-";
	selectWindow("Mask");
	rename(maskName);
	//	save(path+maskName+basename+".tif"); // optional saving the mask
	return;
	}


// make masks for each channel using the saved ROI sets
for (c = 1; c <= 2; c ++)
	{
	channel = d2s(c,0);
	if (!(isOpen("C"+channel+"-"+basename+".tif"))) // open the channel image
		{
		open(path + "C" + channel + "-" + basename + ".tif");
		}
		
	id = getImageID();
	roiManager("Open", path+"RoiSet_C" + channel + "-" + basename + ".zip"); // open the ROI set
	makeMask(id, channel);
	
	selectImage(id); // close the original image
	close();
	}

// multiply masks to find the overlapping areas - result is 255 where both masks are 255, and 0 elsewhere  
imageCalculator("Multiply create","C1-Mask-","C2-Mask-");
rename("Overlap-"+basename+".tif"); // the result window is automatically selected
save(path+"Overlap-"+basename+".tif");

selectWindow("C1-Mask-");
rename("C1-Mask-"+basename+".tif"); // renaming gives us the filename in the results table

selectWindow("C2-Mask-");
rename("C2-Mask-"+basename+".tif");

function AnalyzePartic(image)
	{
	// prints a particle summary for the specified image ID
	run("Analyze Particles...", "size=" + CELLMIN + "-" + CELLMAX + " exclude summarize");
	return;
	}

run("Clear Results");

// get cell counts for channel 1
selectWindow("C1-Mask-"+basename+".tif");
id1 = getImageID();
AnalyzePartic(id1);
selectImage(id1);
close();

// get cell counts for channel 2
selectWindow("C2-Mask-"+basename+".tif");
id2 = getImageID();
AnalyzePartic(id2);
selectImage(id2);
close();

// get cell counts for overlap
selectWindow("Overlap-"+basename+".tif");
idOverlap = getImageID();
AnalyzePartic(idOverlap);
selectImage(idOverlap);
run("Close");

// read data from the Summary window
selectWindow("Summary"); 
lines = split(getInfo(), "\n"); 
headings = split(lines[0], "\t"); 
C1Values = split(lines[1], "\t"); 
C1Name = C1Values[0];
C2Values = split(lines[2], "\t"); 
C2Name = C2Values[0];
OverlapValues = split(lines[3], "\t"); 

// convert strings to integers
C1Count = parseInt(C1Values[1]);
C2Count = parseInt(C2Values[1]);
OverlapCount = parseInt(OverlapValues[1]);

// calculate the percent overlap
C1withC2 = OverlapCount/C1Count;
C2withC1 = OverlapCount/C2Count;

// create a new table with all colocalization data
// format: image name, n cells, n cells colocalized with other label, % colocalized with other label
run("New... ", "name=[Cell_Colocalization] type=Table"); 
print("[Cell_Colocalization]", "Filename\tChannel\tTotal Cells\tColocalized Cells\tFraction Colocalized\n");
print("[Cell_Colocalization]", C1Name+"\t1\t" + C1Count+ "\t"+ OverlapCount + "\t" + C1withC2);
print("[Cell_Colocalization]", C2Name+"\t2\t" + C2Count+ "\t"+ OverlapCount + "\t" + C2withC1);

// save the table
selectWindow("Cell_Colocalization");
saveAs("Text", path+"coloc-"+basename+".xls");

// clean up
selectWindow("Cell_Colocalization");
run("Close");
selectWindow("Summary"); 
run("Close");
selectWindow("ROI Manager");
run("Close");



