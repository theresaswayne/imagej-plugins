macro 'convert LIF tiff' {

/*
 * - converts LIF files into TIFF. 
 * - the macro ask for an input folder that should contain 1 or more lif files. Nothing else.
 * - each series is saved as one tiff. The name of the series is appended to the file name.
 * - if the series has more the one frame, a Max Intensity projection is performed before saving
 * - the TIFFs are saved in a folder generated by the macro
 * 
 * Martin Hoehne, August 2015
 * 
 *  Update October 2015: works for Multichannel images as well
 *  Update October 2016 by azim58: works for lif which contain images that are not stacks.  The script will also not overwrite images if they have the same name.
 *  Update March 2017 by Theresa Swayne: bit depth is preserved, Z projection is optional 
 *  
 */

Z_PROJECT = "True"; // "True" to make a max projection; "False" to leave z stacks intact

run("Bio-Formats Macro Extensions");

dir1 = getDirectory("Choose folder with lif files ");
list = getFileList(dir1);

setBatchMode(true);

// create folders for the tifs
dir1parent = File.getParent(dir1);
dir1name = File.getName(dir1);
dir2 = dir1parent+File.separator+dir1name+"--Tiff_MIP";
if (File.exists(dir2)==false) 
	{
	File.makeDirectory(dir2); // new directory for tiff
    }
 
for (i=0; i<list.length; i++) 
	{
    showProgress(i+1, list.length);
    print("processing ... "+i+1+"/"+list.length+"\n         "+list[i]);
    path=dir1+list[i];

    //how many series in this lif file?
    Ext.setId(path);//-- Initializes the given path (filename).
    Ext.getSeriesCount(seriesCount); //-- Gets the number of image series in the active dataset.
    
    for (j=1; j<=seriesCount; j++) 
    	{
        run("Bio-Formats", "open=path autoscale color_mode=Default view=Hyperstack stack_order=XYCZT series_"+j);
        name=File.nameWithoutExtension;

	    //retrieve name of the series from metadata
        text=getMetadata("Info");
        n1=indexOf(text," Name = ")+8;// the Line in the Metadata reads "Series 0 Name = ". Complete line cannot be taken, because
                                      // The number changes of course. But at least in the current version of Metadata this line is the 
                                      // only occurence of " Name ="
        n2=indexOf(text,"SizeC = ");  // this is the next line in the Metadata
        seriesname=substring(text, n1, n2-2);
		seriesname=replace(seriesname,"/","-");
		rename(seriesname);
		
	    // project and save
    	getDimensions(width, height, channels, slices, frames); // check if is a stack of any kind
    	if (slices>1) // it is a z stack
    		{
        	if (Z_PROJECT == "True")
        		{
        		run("Z Project...", "projection=[Max Intensity]");
        		selectWindow("MAX_"+seriesname);
        		}
	        saveAs("Tiff", dir2+File.separator+name+"_"+seriesname+"_MIP_"+j+".tif");    
    		}
        else
        	{
	        saveAs("Tiff", dir2+File.separator+name+"_"+seriesname+"_MIP_"+j+".tif");
        	}    
        run("Close All");
        run("Collect Garbage");
    	}
	}
showMessage(" -- finished --");    
run("Close All");
setBatchMode(false);

} // macro